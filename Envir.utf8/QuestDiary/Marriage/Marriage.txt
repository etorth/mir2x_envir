;;Marriage.txt
[@Marrage]
{
begin
	#Define N0{START}          N0   ;0:绝澜, 1:矫累
	#Define N1{WEDDING}        N1
	#Define N3{TIME}           N3
	#Define N4{TRY}            N4
	#Define N5{WAITINGTIMEOUT} N5
	#Define N6{MAN}            N6   ;1:巢磊 脚没窃
	#Define N7{GIRL}           N7   ;1:咯磊 脚没窃

	#Define B1{MANNAME}        B1   ;脚尔捞抚
	#Define B2{GIRLNAME}       B2   ;脚何捞抚
	#Define B3{INPUTGIRLNAME}  B3   ;脚尔捞 涝仿茄 脚何捞抚

	#IF
		Equal   N0{START}   1
	#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_1

	#ELSESAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_2


	#IF
		Equal   B1{MANNAME}     %USERNAME
	#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_3


	#IF
		IsAdmin
	#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_4

end
;--------------------------------------------------------------------
   [@TakeMarriedRing]
   begin
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_5

   end
;--------------------------------------------------------------------
	[@ExchangeMarriedRing]
	begin
		#IF
			checkgold 200000
		#ACT
			take 金币 200000
			CancelMarriageRing
			SysMsg "已丧失了结婚戒指的功能"
			close
		#ELSESAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_6

   end
;--------------------------------------------------------------------
	[@ResetWeddingVariables]
	begin
		#ACT
			goto @cancelWedding
			close
	end
;--------------------------------------------------------------------
	[@PreCancelWedding]
	begin
		#ACT
			SysMsg "您已经取消了结婚申请。"
			goto @CancelWedding
	end
;--------------------------------------------------------------------
	[@CancelWedding]
	begin
		#ACT
			Mov  N0{START}      0
			Mov  N1(WEDDING}    0
			Mov  N3{TIME}       0
			Mov  N4{TRY}        0
			Mov  N5{WAITINGTIMEOUT} 0
			Mov  N6{MAN}        0
			Mov  N7{GIRL}       0
			Mov  B1{MANNAME}    ""
			Mov  B2{GIRLNAME}   ""
			Mov  B3{INPUTGIRLNAME}  ""
			Mov  B4 ""
			close
			ClearDelayGoto
	end
;--------------------------------------------------------------------
	[@wantGetMarried]       ;搬去窍妨绰 荤恩(巢/咯) 努腐
	begin
		#IF
			!CheckLevel 22
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_7

		#ACT
			break

		#IF
			CheckMarriage
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_8

		#ACT
			break

		#IF
			Gender [man]
		#ACT
			goto @wantGetMarried_man
		#ELSEACT
			goto @wantGetMarried_woman
   end
;---------------------------------------------------------
;;搬去窍妨绰 巢磊
	[@wantGetMarried_man]
	begin
		#IF
			Equal   B1{MANNAME}     ""
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_9

		#ELSESAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_10

	end
;---------------------------------------------------------
;;脚何狼 捞抚阑 涝仿 窃.
	[@@InputMarried_Woman]
	begin
		#IF
			Mov     B5  %INPUTSTR     ;B5 烙矫函荐
			!Equal  B5  ""
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_11

	end
;---------------------------------------------------------
	[@ProcessMarried]
	begin
		#IF
			Equal   N6{MAN}     1
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_12

		#ACT
			break

		#IF
			!Equal   B1{MANNAME} ""
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_13

		#ACT
			break

		#IF
			!Equal   B3{INPUTGIRLNAME} ""
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_14

		#ACT
			break

		#IF
			!CheckGold   500000
		#ACT
			SysMsg  "结婚费用不够。"
			close
			break

		#IF
		#ACT
			mov     N1{WEDDING}         1           ;搬去 柳青 窜拌
			mov     B1{MANNAME}         %USERNAME   ;脚何捞抚篮 B2
			mov     B3{INPUTGIRLNAME}   %INPUTSTR
			mov     N6{MAN}             1           ;脚尔 脚没 场
			mov     N5{WAITINGTIMEOUT}  0           ;脚没窍绊 老沥 矫埃捞 瘤唱搁 秒家凳
			Delaygoto [local] 5 @startWeddingloop
			SysMsg  "已完成结婚申请，等新娘也来申请结婚，就会结算结婚费用。"
			close
	end
;-----------------------------------------------------------
;搬去窍妨绰 咯磊
	[@wantGetMarried_woman]
	begin
		#IF                                             ;巢磊啊 刚瘤 脚没窍绊 扁促妨具 窃
			equal   N6{MAN}             1
			equal   B3{INPUTGIRLNAME}   %USERNAME
		#ELSEACT
			SysMsg  "新郎尚未申请结婚。"
			close
			break

		#IF
			CheckGold 500000
		#ACT
			Take    金币          500000
			mov     N7{GIRL}            1                   ;咯磊 霖厚场
			mov     B2{GIRLNAME}        %USERNAME           ;脚何 捞抚 涝仿
			SysMsg  "已完成结婚申请。"
			close
		#ELSEACT
			SysMsg  "结婚费用不够。"
			close
	end
	;------------------------------------------------------------
	[@startWeddingloop]     ;巢磊率俊辑 导
	begin
		#IF
			Equal   N0{START}   1
		#ACT
			break

		#IF
			Inc     N5{WAITINGTIMEOUT}      1
			Large   N5{WAITINGTIMEOUT}      10   ;360
		#ACT
			SysMsg  "已完成结婚申请，等新娘也来申请结婚，就会结算结婚费用。"
			goto @CancelWedding
			break

		#IF
			Equal   N6{MAN}     1
			Equal   N7{GIRL}    1
		#ELSEACT
			SysMsg  "正在等候新娘申请结婚。"
			Delaygoto [local] 5 @startWeddingloop
			break

		#IF
			!CheckGold   500000
		#ACT
			SysMsg  "结婚费用不够。"
			Delaygoto [local] 5 @startWeddingloop
			break

		#IF
		#ACT
			Take    金币          500000
			Mov     N0{START}   1               ;搬去侥 矫累沁澜.
			Mov     N3{TIME}    0               ;搬去侥 鸥烙酒眶 函荐
			EventMsg  "0" "马上就会举行新郎 <$OUTPUT(B1)>先生和新娘<$OUTPUT(B2)>女士的婚礼。"
			SysMsg    "婚礼相关准备已做好。"
			Delaygoto [local] 10  @weddingCeremony      ;搬去侥捞 鸥烙酒眶登菌绰瘤 八荤.
			close
	end

;--------------------------------------------------------------
;搬去侥 柳青.

	[@weddingCeremony]
	begin
		#IF
			Equal N1{WEDDING} 0  ;搬去侥捞 秒家凳
		#ACT
			break

		#IF
			Inc    N3{TIME} 1
			Large  N3{TIME} 360
		#ACT
			EventMsg  "0"  "预约的婚礼时间已到。"
			break  ;搬去侥 鸥烙 酒眶.

		#IF
			Equal  N1{WEDDING}   1
		#ACT
			EventMsg   "0"  "现在开始举行新郎 <$OUTPUT(B1)>先生和新娘<$OUTPUT(B2)>女士的婚礼。"
			Mov  N1{WEDDING}  2
			Delaygoto [local]   10  @weddingCeremony
			break

		#IF
			Equal  N1{WEDDING}  2
		#ACT
			EventMsg  "0"  "新郎 <$OUTPUT(B1)> 发誓会在人生风风雨雨的旅途中永远爱新娘<$OUTPUT(B2)>吗？ 请用“是/不是”来简短回答。 "
			Mov  N4{TRY}    5      ;5锅狼 扁雀甫 淋.
			CaptureSaying   %B1  @ListenSayYes_man
			Mov   N1{WEDDING}  3
			Delaygoto [local]   10  @weddingCeremony
			break

		#IF
			Equal N1{WEDDING}  3
		#ACT
			EventMsg  "0"  "新郎请用“是/不是”来回答。"
			Delaygoto [local]   10  @weddingCeremony
			break

		#IF
			Equal  N1{WEDDING}  4
		#ACT
			Eventmsg   "0"  "新娘 <$OUTPUT(B2)>发誓会在人生风风雨雨的旅途中永远爱新郎 <$OUTPUT(B1)> 吗？请用“是/不是”来简短回答。"
			Mov  N4{TRY}    5      ;5锅狼 扁雀甫 淋.
			CaptureSaying  %B2  @ListenSayYes_woman
			Mov  N1{WEDDING}  5
			Delaygoto [local]   10  @weddingCeremony
			break

		#IF
			Equal N1{WEDDING}  5
		#ACT
			EventMsg  "0"  "新娘请用“是/不是”来回答。"
			Delaygoto [local]   10  @weddingCeremony
			break

		#IF
			Equal  N1{WEDDING}  6
		#ACT
			Eventmsg "0" "现在新郎新娘交换作为结婚信物的戒指。请交换戒指。"
			GetMarried  %B1  %B2
			mov   N1{WEDDING}  7
			Delaygoto [local]   10  @weddingCeremony
			break

;搬去茄 荤恩狼 SpouseName捞 汲沥登绊, 哭颊俊 搬去馆瘤啊 绝绰 版快俊绰 硅快磊俊霸辑
;罐篮 馆瘤啊 磊悼栏肺 搬去 馆瘤啊 等促. 搬去馆瘤绰 公炼扒 哭颊俊 哺柳促.

		#IF
			Equal N1{WEDDING} 7
			CheckMarriageRing  %B1
			CheckMarriageRing  %B2
		#ACT
			EventMsg  "0" "至此，新郎 <$OUTPUT(B1)>先生和新娘 <$OUTPUT(B2)> 女士的婚礼结束。祝二位白头偕老，永远幸福！"
			goto @cancelWedding
			Break

		#IF
		#ACT
			Delaygoto [local]   10  @weddingCeremony
	end

	[@GetFace]
	begin
		#IF
			CheckJob  Warrior
		#ACT
			mov  P1 0

		#IF
			CheckJob  Wizard
		#ACT
			mov p1 1

		#IF
			CheckJob  Taoist
		#ACT
			mov p1 2

		#IF
		#ACT
			mul p1 2 ;巢磊绰 娄荐, 咯磊绰 圈荐
			mov p1 p9

		#IF
			gender woman
		#ACT
			inc p1

		#IF
		#ACT
			inc p1 20000  ;荤恩倔奔篮 20000锅 捞惑烙
		#SAY
			<$OUTPUT(p1)>
	end
;-----------------------------------------------------------
;巢磊啊 窍绰 富阑 钎矫窍绊 八荤窃.
	[@ListenSayYes_man]
	begin
		#IF
			Equal N1{WEDDING}  0  ;搬去捞 秒家登搁.
		#ACT
			CaptureSaying ""
			break

		#IF
		#ACT
			mov  B9 %INPUTSTR
			mov  P1 %@GetFace
			EventMsg "0"  "<$OUTPUT(B9)>" %P1

		#IF
			Equal  N1{WEDDING}  3
			Equal  B9  "是"
		#ACT
			Mov  B9  ""
			EventMsg  "0"  "新郎 <$OUTPUT(B1)> 先生已回答“是”。"
			Mov  N1{WEDDING}  4
			CaptureSaying  ""  ;秒家窃.
			break

		#IF
			Equal  N1{WEDDING}  3
			Equal  B9  "不是"
		#ACT
			Mov  B9  ""
			EventMsg  "0"  "新郎 <$OUTPUT(B1)> 先生回答“不是”"
			EventMsg  "0"  "这场婚事视为无效。"
			goto @cancelWedding  ;搬去 秒家
			CaptureSaying  ""  ;秒家窃.
			Break

		#IF
			Equal  N1{WEDDING} 3
			!Equal  B9  ""
		#ACT
			EventMsg  "0" "新郎请用“是/不是”来回答。"

		#IF
			Large N4{TRY}   0
		#ACT
			SysMsg "只剩下<$OUTPUT(N4)>次的机会。请用“是/不是”来回答。"
		#ELSEACT
			EventMsg  "0"  "婚礼已取消。"
			goto @cancelWedding
			CaptureSaying  ""  ;秒家窃.
			Break

		#IF
		#ACT
			Dec   N4{TRY}   1
	end
;-----------------------------------------------------------
;咯啊 窍绰 富阑 钎矫窍绊 八荤窃.
	[@ListenSayYes_woman]
	begin
		#IF
			Equal N1{WEDDING}  0  ;搬去捞 秒家登搁.
		#ACT
			CaptureSaying ""
			break

		#IF
		#ACT
			mov  B9 %INPUTSTR
			mov  P1 %@GetFace
			EventMsg "0"  "<$OUTPUT(B9)>" %P1

		#IF
			Equal  N1{WEDDING}  5
			Equal  B9  "是"
		#ACT
			Mov  B9  ""
			EventMsg  "0"  "新娘 <$OUTPUT(B2)> 女士已回答“是”。"
			Mov  N1{WEDDING}  6
			CaptureSaying  ""  ;秒家窃.
			break

		#IF
			Equal  N1{WEDDING}  5
			Equal  B9  "不是"
		#ACT
			Mov  B9  ""
			EventMsg  "0"  "新娘 <$OUTPUT(B2)>女士回答“不是”。"
			EventMsg  "0"  "这场婚事视为无效。"
			CaptureSaying  ""  ;秒家窃.
			goto @cancelWedding  ;搬去 秒家
			Break

		#IF
			Equal  N1{WEDDING} 5
			!Equal  B9  ""
		#ACT
			EventMsg  "0" "新娘请用“是/不是”来回答。"

		#IF
			Large N4{TRY}   0
		#ACT
			SysMsg "<只剩下$OUTPUT(N4)>次的机会。请用“是/不是”来回答。"
		#ELSEACT
			EventMsg  "0"  "婚礼已取消。"
			goto @cancelWedding
			CaptureSaying  ""  ;秒家窃.
			Break

		#IF
		#ACT
			Dec   N4{TRY}   1
	end
;----------------------------------------------------------------
	[@wantDivorce]
	begin
		#IF
   		CheckMarriage
		#SAY
	#INCLUDE [../Convert_Def/QuestDiary/Marriage/Marriage.txt] @Marriage_15


		[@divorcenow]
		#IF
			checkgold 5000000
		#ACT
			take 金币 5000000
			Divorce
			close
	end
}
